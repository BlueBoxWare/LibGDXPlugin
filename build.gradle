buildscript {

    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }

}

plugins {
    id "org.jetbrains.intellij" version "0.2.17"
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'maven-publish'

group 'com.gmail.blueboxware'
version pluginVersion

intellij {
    if (Boolean.valueOf(isAndroidStudio)) {
        version = asVersion
        plugins = ["org.jetbrains.kotlin:$asKotlinPlugin", 'Groovy', 'properties']
        alternativeIdePath = System.getenv("ANDROID_STUDIO_PATH")
    } else {
        version = ideaVersion
        plugins = ['Kotlin', 'Groovy', 'properties']
    }

    pluginName = 'LibGDX Inspections'
    updateSinceUntilBuild = false
    downloadSources =  true
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources.srcDir 'resources'
        java {
            srcDir 'gen'
            exclude 'com/gmail/blueboxware/libgdxplugin/annotations/**'
        }
    }
    annotations {
        java {
            srcDir 'src/main/java'
        }
    }
}

compileAnnotationsJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

test {
    systemProperty 'idea.home.path', System.getenv("LIBGDXPLUGIN_IDEA")
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlinVersion"
    compile files("${System.properties['java.home']}/../lib/tools.jar")

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task annotationsJar(type: Jar) {
    baseName 'libgdxpluginannotations'
    from sourceSets.annotations.output
    include 'com/gmail/blueboxware/libgdxplugin/annotations/**'
    version pluginVersion
}

task annotationsSourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.annotations.allSource
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    include tasks.getByName("annotationsJar").includes
    baseName tasks.getByName("annotationsJar").property('baseName')
    version pluginVersion

}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "blueboxware"
            name "Blue Box Ware"
            email "blueboxware@gmail.com"
        }
    }
}

model {
    tasks.generatePomFileForAnnotationsJarPublication {
        destination = file("$buildDir/" + tasks.getByName("annotationsJar").property('baseName') + "-" + tasks.getByName("annotationsJar").property('version') + ".pom")
    }
}

publishing {
    publications {
        AnnotationsJar(MavenPublication) {
            artifact annotationsJar
            artifact annotationsSourcesJar
            groupId project.group
            artifactId tasks.getByName("annotationsJar").property('baseName')
            version pluginVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An annotation for use with LibGDXPlugin for IntelliJ')
                root.appendNode('name', tasks.getByName("annotationsJar").property('baseName'))
                root.appendNode('url', 'https://github.com/BlueBoxWare/LibGDXPlugin')
                root.children().last() + pomConfig
            }
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}