buildscript {
    apply from: 'versions.gradle'

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }

    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "gradle.plugin.org.jetbrains.intellij.plugins:gradle-intellij-plugin:$intellijPluginVersion"
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.blueboxware.tocme' version '1.0'
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

group 'com.gmail.blueboxware'
version pluginVersion

intellij {

    plugins = ['Kotlin', 'Groovy', 'properties']

    if (useStudio) {
        version = studioVersion
        if (runStudio) {
            plugins = ["Kotlin", 'Groovy', 'properties']
            alternativeIdePath = System.getenv("ANDROID_STUDIO_PATH")
        }
    } else {
        version = ideaVersion
    }


    if (version == 'LATEST-EAP-SNAPSHOT' ||  VersionNumber.parse(version) >= VersionNumber.parse('2019.2')) {
        plugins = plugins + 'java'
    }

    pluginName = 'LibGDX Plugin'
    updateSinceUntilBuild = false
    downloadSources =  true

    // https://github.com/JetBrains/gradle-intellij-plugin/issues/230
    instrumentCode = false
}

runIde {
    maxHeapSize = "2g"
    systemProperties = [
            'idea.ProcessCanceledException': 'disabled'
    ]
}

tocme {
    doc("README.md")
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources.srcDir 'resources'
        java {
            srcDirs += 'src/main/kotlin'
            srcDirs += 'gen'
            exclude 'com/gmail/blueboxware/libgdxplugin/annotations/**'
        }
    }
    annotations {
        java {
            srcDir 'src/main/java'
        }
    }
}

compileAnnotationsJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

test {
    systemProperty 'idea.home.path', System.getenv("LIBGDXPLUGIN_IDEA")
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile files("${System.properties['java.home']}/../lib/tools.jar")

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task annotationsJar(type: Jar) {
    baseName 'libgdxpluginannotations'
    from sourceSets.annotations.output
    include 'com/gmail/blueboxware/libgdxplugin/annotations/**'
    version pluginVersion
}

task annotationsSourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.annotations.allSource
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    include tasks.getByName("annotationsJar").includes
    baseName tasks.getByName("annotationsJar").property('baseName')
    version pluginVersion

}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "blueboxware"
            name "Blue Box Ware"
            email "blueboxware@gmail.com"
        }
    }
}

model {
    tasks.generatePomFileForAnnotationsJarPublication {
        destination = file("$buildDir/" + tasks.getByName("annotationsJar").property('baseName') + "-" + tasks.getByName("annotationsJar").property('version') + ".pom")
    }
}

publishing {
    publications {
        AnnotationsJar(MavenPublication) {
            artifact annotationsJar
            artifact annotationsSourcesJar
            groupId project.group
            artifactId tasks.getByName("annotationsJar").property('baseName')
            version pluginVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Annotations for use with LibGDXPlugin for IntelliJ')
                root.appendNode('name', tasks.getByName("annotationsJar").property('baseName'))
                root.appendNode('url', 'https://github.com/BlueBoxWare/LibGDXPlugin')
                root.children().last() + pomConfig
            }
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}