buildscript {
    apply from: 'versions.gradle'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.blueboxware.tocme' version '1.0'
    id "org.jetbrains.intellij" version "1.0"
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

group 'com.gmail.blueboxware'
version pluginVersion

intellij {

    plugins = ['java', 'Kotlin', 'Groovy', 'properties']

    if (useStudio) {
        version = studioVersion
    } else {
        version = ideaVersion
    }

    pluginName = 'LibGDX Plugin'
    updateSinceUntilBuild = false
    downloadSources =  true

}

// TODO
tasks.buildSearchableOptions {
    enabled = false
}

runIde {
    maxHeapSize = "2g"
    systemProperties = [
            'idea.ProcessCanceledException': 'disabled'
    ]
    if (useStudio && runStudio) {
        alternativeIdePath = System.getenv("ANDROID_STUDIO_PATH")
    }
}

tocme {
    doc("README.md")
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources.srcDir 'resources'
        java {
            srcDirs += 'src/main/kotlin'
            srcDirs += 'gen'
            exclude 'com/gmail/blueboxware/libgdxplugin/annotations/**'
        }
    }
    annotations {
        java {
            srcDir 'src/main/java'
        }
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileAnnotationsJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

test {
    systemProperty 'idea.home.path', System.getenv("LIBGDXPLUGIN_IDEA")
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task annotationsJar(type: Jar) {
    baseName 'libgdxpluginannotations'
    from sourceSets.annotations.output
    include 'com/gmail/blueboxware/libgdxplugin/annotations/**'
    version pluginVersion
}

task annotationsSourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.annotations.allSource
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    include tasks.getByName("annotationsJar").includes
    baseName tasks.getByName("annotationsJar").property('baseName')
    version pluginVersion

}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "blueboxware"
            name "Blue Box Ware"
            email "blueboxware@gmail.com"
        }
    }
}

model {
    tasks.generatePomFileForAnnotationsJarPublication {
        destination = file("$buildDir/" + tasks.getByName("annotationsJar").property('baseName') + "-" + tasks.getByName("annotationsJar").property('version') + ".pom")
    }
}

publishing {
    publications {
        AnnotationsJar(MavenPublication) {
            artifact annotationsJar
            artifact annotationsSourcesJar
            groupId project.group
            artifactId tasks.getByName("annotationsJar").property('baseName')
            version pluginVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Annotations for use with LibGDXPlugin for IntelliJ')
                root.appendNode('name', tasks.getByName("annotationsJar").property('baseName'))
                root.appendNode('url', 'https://github.com/BlueBoxWare/LibGDXPlugin')
                root.children().last() + pomConfig
            }
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
