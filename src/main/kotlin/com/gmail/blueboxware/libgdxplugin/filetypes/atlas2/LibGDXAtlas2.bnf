{

    parserClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.Atlas2Parser'
    parserUtilClass="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.Atlas2ParserUtil"

    psiPackage = 'com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi'
    psiImplPackage = 'com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.impl'

    elementTypeHolderClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.Atlas2ElementTypes'
    elementTypeClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.Atlas2ElementType'

    psiClassPrefix = 'Atlas2'

    psiVisitorName = 'Atlas2ElementVisitor'

    tokenTypeClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.Atlas2TokenType'

    implements("page|region|field") = 'com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.Atlas2NamedElement'

    tokens = [
        COLON = ':'
        COMMA = ','

        THEADER = 'header'
        TKEY = 'key'
        TVALUE = 'value'

        EMPTY_LINE = ''
    ]

}

atlas2 ::= EMPTY_LINE* field* page*

page ::= header field* region* (EMPTY_LINE | <<eof>>) {
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.Atlas2PageOwner"
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.impl.mixins.Atlas2PageMixin"
}

region ::= header field* {
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.Atlas2ImageOwner"
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.impl.mixins.Atlas2RegionMixin"
}

field ::= key  COLON (value | COMMA)* {
    methods=[
        keyElement="key"
    ]
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.Atlas2ValuesOwner"
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.impl.mixins.Atlas2FieldMixin"
}

value ::= TVALUE {
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.Atlas2ValueOwner"
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.impl.mixins.Atlas2ValueMixin"
}

header ::= THEADER {
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.Atlas2ValueOwner"
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.impl.mixins.Atlas2HeaderMixin"
}

key ::= TKEY {
name="keyElement"
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.Atlas2ValueOwner"
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.atlas2.psi.impl.mixins.Atlas2KeyMixin"
}




