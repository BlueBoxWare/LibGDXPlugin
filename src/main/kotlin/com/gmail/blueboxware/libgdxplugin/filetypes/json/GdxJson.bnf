{

  parserClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.GdxJsonParser'
  parserUtilClass="com.gmail.blueboxware.libgdxplugin.filetypes.json.GdxJsonParserUtil"
  psiPackage = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.psi'
  psiImplPackage = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl'

  elementTypeHolderClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.GdxJsonElementTypes'
  elementTypeClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.GdxJsonElementType'
  psiClassPrefix = "GdxJson"
  psiVisitorName = "GdxJsonElementVisitor"

  tokenTypeClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.GdxJsonTokenType'

  implements("jobject|array|value|property") = "com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.GdxJsonElement"
  implements("string|number_value") = "com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.GdxJsonLiteral"
  implements("property_name") = "com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.GdxJsonString"

  name("jobject") = 'object'
  name("property_name") = 'property name'

  tokens = [
        L_CURLY='{'
        R_CURLY='}'
        L_BRACKET='['
        R_BRACKET=']'
        COMMA=','
        COLON=':'
        DOUBLE_QUOTE='"'
        SLASH='/'
        BACK_SLASH='\'
        ASTERIX='*'

        LINE_COMMENT='LINE COMMENT'
        BLOCK_COMMENT='BLOCK COMMENT'

        DOUBLE_QUOTED_STRING='QUOTED STRING'
        UNQUOTED_STRING='UNQUOTED STRING'

    ]

}

json ::=  value 

value ::= jobject | array | string | number_value {
    methods=[
        getValue
        isPropertyValue
    ]
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonValueMixin"
}

jobject ::= '{'  property?  (separator  property)* ','?  '}' {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonJobjectMixin"
    methods=[
        getProperty
        getProperties
    ]
}

array ::= '['  value?  (separator  value )* ','?  ']' {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonArrayMixin"
}

property ::= property_name  ':'  value {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonPropertyMixin"
    pin = 1
    recoverWhile = not_brace_or_separator
}

private not_brace_or_separator ::= !( '}' | separator)

string ::= DOUBLE_QUOTED_STRING | UNQUOTED_STRING {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonStringMixin"
    methods=[
        isQuoted
        isKeyword

        DOUBLE_QUOTED_STRING=""
    ]
}

number_value ::= NUMBER {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonNumberValueMixin"
}

property_name ::= DOUBLE_QUOTED_STRING | UNQUOTED_STRING | NUMBER {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonPropertyNameMixin"
    methods=[
        getValue
        isQuoted

        DOUBLE_QUOTED_STRING=""
    ]
}

external separator ::= parseSeparator