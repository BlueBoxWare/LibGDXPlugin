{
  parserClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.GdxJsonParser'
  parserUtilClass="com.gmail.blueboxware.libgdxplugin.filetypes.json.GdxJsonParserUtil"
  psiPackage = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.psi'
  psiImplPackage = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl'

  elementTypeHolderClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.GdxJsonElementTypes'
  elementTypeClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.GdxJsonElementType'
  psiClassPrefix = "GdxJson"
  psiVisitorName = "GdxJsonElementVisitor"

  tokenTypeClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.json.GdxJsonTokenType'

  implements("jobject|array|value|property|name|string") = "com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.GdxJsonElement"

  tokens = [
        L_CURLY='{'
        R_CURLY='}'
        L_BRACKET='['
        R_BRACKET=']'
        COMMA=','
        COLON=':'
        DOUBLE_QUOTE='"'
        SLASH='/'
        BACK_SLASH='\'
        ASTERIX='*'
        ANY_CHAR='regexp:.'
    ]

}

json ::= comment* value comment*

value ::= '{' jobject | '[' array | string {
    methods=[
        getValue
    ]
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonValueMixin"
}

jobject ::= comment* property? comment* (separator comment* property comment*)* ','? comment* '}' {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonJobjectMixin"
}

array ::= comment* value? comment* (separator comment* value comment*)* ','? comment* ']' {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonArrayMixin"
}

property ::= property_name comment* ':' comment* value {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonPropertyMixin"
}

string ::= quoted_string | unquoted_value_string {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonStringMixin"
}

property_name ::= quoted_string | unquoted_name_string {
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.json.psi.impl.mixins.GdxJsonPropertyNameMixin"
}

private quoted_string ::= '"' quoted_chars '"'

external unquoted_value_string ::= parseUnquotedValueString
external quoted_chars ::= parseQuotedChars
external unquoted_name_string ::= parseUnquotedNameString
external comment ::= parseComment
external separator ::= parseSeparator