//noinspection RegExpRedundantEscapeForFile
{
  parserClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.skin.SkinParser'
  parserUtilClass="com.gmail.blueboxware.libgdxplugin.filetypes.skin.SkinParserUtil"
  psiPackage = 'com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi'
  psiImplPackage = 'com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl'

  elementTypeHolderClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.skin.SkinElementTypes'
  elementTypeClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.skin.SkinElementType'
  psiClassPrefix = "Skin"
  psiVisitorName = "SkinElementVisitor"

  tokenTypeClass = 'com.gmail.blueboxware.libgdxplugin.filetypes.skin.SkinTokenType'

  implements("value|property_name|resource_name|property_value|class_name|resources") = "com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinElement"
  extends("value|resources") = "com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.SkinElementImpl"

  implements("resource|property|class_specification") = "com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinNamedElement"

  extends("string_literal|array|object")=value

  extends("class_specification")="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.SkinElementImpl"

  tokens = [
        L_CURLY='{'
        R_CURLY='}'
        L_BRACKET='['
        R_BRACKET=']'
        COMMA=','
        COLON=':'

        LINE_COMMENT='regexp://.*'
        BLOCK_COMMENT='regexp:/\*([^*]|\*+[^*/])*(\*+/)?'

        DOUBLE_QUOTED_STRING="regexp:\"([^\\\"\r\n]|\\[^\n\r])*\"?"
        UNQUOTED_STRING="regexp:([^\\\":,{}\[\]/\r\n\t ]|\\[^\n\r])([^\\:}\]/,\r\n\t ]|/[^/*:]|\\[^\n\r])*"
    ]

}

skin ::= '{'  class_specification? (separator class_specification)* optionalComma '}' {
    pin = 1
}

class_specification ::= class_name class_spec_colon class_spec_lbrace resources '}' {
    pin = 1
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinClassOwner"
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinClassSpecificationMixin"
}

private class_spec_colon ::= ':' {
    recoverWhile = recover_class_colon
}
private recover_class_colon ::= !'{'

private class_spec_lbrace ::= '{' {
    recoverWhile = recover_class_lbrace
}
private recover_class_lbrace ::= !(string_literal | '}')

class_name ::= string_literal {
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinClassNameOwner"
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinClassNameMixin"
}

resources ::= resource? (separator resource)* optionalComma {
    pin = 1
    recoverWhile = recover_class
}
private recover_class ::= !'}'

resource ::= resource_name ':' (object | string_literal) {
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinResourceOwner"
    mixin = "com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinResourceMixin"
    pin = 1
}

resource_name ::= string_literal {
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinResourceNameOwner"
    mixin = "com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinResourceNameMixin"
}

object ::= '{' resource_list '}' {
    pin = 2
    implements="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinObjectOwner"
    mixin="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinObjectMixin"
}

private resource_list ::= property? (separator property)* optionalComma {
    recoverWhile = recover_object
}
private recover_object ::= !'}'

property ::= property_name ':' property_value {
      pin = 1
      recoverWhile = recover_property
      implements="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinPropertySpecification"
      mixin="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinPropertyMixin"
}
private recover_property ::= !(separator | '}')

property_name ::= string_literal {
      implements="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinValueOwner"
      mixin="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinPropertyNameMixin"
}

property_value ::= value {
      implements="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinPropertyOwner"
      mixin = "com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinPropertyValueMixin"
}

value ::= object | array | string_literal {
        implements="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinPropertyValueOwner"
        mixin="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinValueMixin"
}

array ::= '[' value? (separator value)* optionalComma ']' {
        pin=2
        mixin="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinArrayMixin"
}

string_literal ::= DOUBLE_QUOTED_STRING | UNQUOTED_STRING {
      methods=[
        DOUBLE_QUOTED_STRING=""
        UNQUOTED_STRING=""
      ]
      implements="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.SkinStringOwner"
      mixin="com.gmail.blueboxware.libgdxplugin.filetypes.skin.psi.impl.mixins.SkinStringLiteralMixin"
}

external private separator ::= parseSeparator
external private optionalComma ::= parseOtionalComma
